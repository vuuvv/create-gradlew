plugins {
    id "org.springframework.boot" version "$VERSION_SPRING_BOOT" apply false
    id "io.spring.dependency-management" version "$VERSION_DEPENDENCY_MANAGEMENT" apply false
}

allprojects {
    group = "com.vuuvv"
    version = "0.0.1-SNAPSHOT"
}

subprojects {
    it.apply plugin: "io.spring.dependency-management"
    it.apply plugin: "idea"

    if (it.name.endsWith("-web") || it.name == "portal") {
        it.apply plugin: "org.springframework.boot"
        it.apply plugin: "java"
    } else {
        it.apply plugin: "java-library"
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:$VERSION_SPRING_BOOT"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$VERSION_SPRING_CLOUD"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:$VERSION_SPRING_CLOUD_ALIBABA"
        }
        dependencies {
            dependency "org.projectlombok:lombok:$VERSION_LOMBOK"
            dependency "cn.hutool:hutool-all:$VERSION_HUTOOL"
        }
    }

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        implementation "cn.hutool:hutool-all"
    }
}
